buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.2'
    }
}

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
}

group 'com.github.irvifa.beam.pipeline'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'kotlin'
apply plugin: 'com.github.johnrengelman.shadow'
sourceCompatibility = 1.8

ext {
    artifactId = 'beam-pubsub-bigquery-pipeline'
    mainClassName = "com.github.irvifa.beam.pipeline.PubsubToBigqueryPipelineRunner"
}

repositories {
    mavenCentral()
    jcenter()
}

configurations {
    ktlint
}

dependencies {
    ktlint "com.github.shyiko:ktlint:0.29.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    compile "org.apache.beam:beam-sdks-java-core:$beamVersion"
    compile "com.google.cloud:google-cloud-bigquery:$bigqueryVersion"
    compile "org.apache.beam:beam-sdks-java-io-google-cloud-platform:$beamVersion"
    compile "org.apache.beam:beam-runners-google-cloud-dataflow-java:$beamVersion"
    compile "org.apache.beam:beam-runners-direct-java:$beamVersion"
    compile "org.json:json:20190722"
    compile "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    compile "com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonVersion"
    compile "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jacksonVersion"
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'

    testCompile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.30'
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.9.1'
    testImplementation group: 'org.jmock', name: 'jmock-junit4', version: '2.8.3'
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitJupiterVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "io.mockk:mockk:1.9.1"
    testCompile group: 'org.apache.beam', name: 'beam-runners-direct-java', version: '2.19.0'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

shadowJar {
    classifier = null
    mergeServiceFiles()
    exclude 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.SF'
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "src/**/*.kt"
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "-F", "src/**/*.kt"
}
